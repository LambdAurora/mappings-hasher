plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'org.quiltmc'
version '1.0-SNAPSHOT'

// Target JDK 8
tasks.withType(JavaCompile).configureEach {
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    } else {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

application {
    mainClass = 'org.quiltmc.mappings_hasher.Main'
}

repositories {
    mavenCentral()
    maven {
        name = "Quilt"
        url = "https://maven.quiltmc.org/repository/release"
    }
}

configurations {
    implementation {
        extendsFrom shadow
    }
}

dependencies {
    shadow 'org.quiltmc:quilt-json5:1.0.0'
    shadow 'org.quiltmc:lorenz-tiny:3.0.0'
    shadow 'org.cadixdev:lorenz-io-proguard:0.5.6'
    shadow "org.ow2.asm:asm:9.1"
}

tasks.getByName("run").setArgsString(minecraftVersion)

task mappingsJar(type: Jar) {
    dependsOn run
    from "mappings/hashed-${minecraftVersion}.tiny"
    eachFile {
        setName("hashed/mappings.tiny")
    }
}

shadowJar {
    archiveClassifier = null
    configurations  = [project.configurations.shadow]
    relocate "net.fabricmc", "org.quiltmc.mappings_hasher.fabricmc"
    relocate "org.cadixdev", "org.quiltmc.mappings_hasher.cadixdev"
    relocate "org.objectweb.asm", "org.quiltmc.mappings_hasher.asm"
    relocate "me.jamiemansfield", "org.quiltmc.mappings_hasher.jamiemansfield"
}

publishing {
    publications {
        mavenHashed(MavenPublication) {
            groupId = 'org.quiltmc'
            artifactId = 'hashed-mojmap'
            version = minecraftVersion + (System.getenv().SNAPSHOTS_URL ? "-SNAPSHOT" : "")
            artifacts = [mappingsJar]
        }


        mavenHasher(MavenPublication) {
            groupId = 'org.quiltmc'
            artifactId = 'mappings-hasher'
            version = version
            artifacts = [shadowJar]
        }
    }
    repositories {
        def ENV = System.getenv()

        if (ENV.MAVEN_URL) {
            maven {
                url = ENV.MAVEN_URL

                credentials {
                    username = ENV.MAVEN_USERNAME
                    password = ENV.MAVEN_PASSWORD
                }
            }
        } else if (ENV.SNAPSHOTS_URL) {
            maven {
                url = ENV.SNAPSHOTS_URL

                credentials {
                    username = ENV.SNAPSHOTS_USERNAME
                    password = ENV.SNAPSHOTS_PASSWORD
                }
            }
        }
    }
}